### 递归下降分析法
递归下降分析算法本质也是一种自顶向下分析算法，其基本思想如下：

- 对每一个非终结符构造一个分析函数
- 用前看符号指导产生式规则的选择

递归下降分析算法使用“分治法”来提高分析的效率，对于每一个产生式规则，都应该定义一个自己函数。因为在上下文无关文法中，终结符不可能出现在产生式的左边（可以在产生式左边出现终结符的文法叫做上下文有关文法），上下文无关文法中所有的产生式左边只有一个非终结符。所以我们在调用产生式规则的函数后，就分为两种情况：

遇到终结符，因为终结符本质上是token，所以直接把这个终结符和句子中对应位置的token进行比较，判断是否符合即可；符合就继续，不符合就返回

遇到非终结符，此时只需要调用这个非终结符对应的函数即可。在这里函数可能会递归的调用，这也是算法名称的来源。
简单来说，就是遇到非终结符就调用函数，遇到终结符就比较；例如，同样对于文法G：

### 文法
```
<程序> → <分程序>
<分程序> → begin <说明语句表>；<执行语句表> end
<说明语句表> → <说明语句>│<说明语句表> ；<说明语句>
<说明语句> → <变量说明>│<函数说明>
<变量说明> → integer <变量>
<变量> → <标识符>
<标识符> → 
<函数说明> → integer function <标识符>（<参数>）；<函数体>
<参数> → <变量>
<函数体> → begin <说明语句表>；<执行语句表> end
<执行语句表> → <执行语句>│<执行语句表>；<执行语句>
<执行语句> → <读语句>│<写语句>│<赋值语句>│<条件语句>
<读语句> → read(<变量>)
<写语句> → write(<变量>)
<赋值语句> → <变量>:=<算术表达式>
<算术表达式> → <算术表达式>-<项>│<项>
<项> → <项>*<因子>│<因子>
<因子> → <变量>│<常数>│<函数调用>
<常数> → <无符号整数>
<无符号整数> → <数字>│<无符号整数><数字>
<函数调用> -> <标识符>(<算术表达式>)
<条件语句> → if<条件表达式>then<执行语句>else <执行语句>
<条件表达式> → <算术表达式><关系运算符><算术表达式>
<关系运算符>  → <│<=│>│>=│=│<>
```
### 左递归的消除
设文法 G 含有直接左递归的产生式为:

<img src='https://latex.codecogs.com/svg.latex?A%20\rightarrow%20A\alpha_1|A\alpha_2|...|A\alpha_n|\beta' />

消除左递归后的产生式为:

<img src='https://latex.codecogs.com/svg.latex?A%20\rightarrow%20\beta%20A^%27' />
<br/>
<img src='https://latex.codecogs.com/svg.latex?A%20^{%27}%20\rightarrow%20\alpha_1A^{%27}|\alpha_2A^{%27}|...|\alpha_nA^{%27}|%20\varepsilon' />


### 消除左递归后的文法
```
<程序> → <分程序>
<分程序> → begin <说明语句表>；<执行语句表> end
<说明语句表> -> <说明语句><说明语句表_s>
<说明语句表_s> -> ;<说明语句><说明语句表_s>|<空>
<说明语句> → <变量说明>│<函数说明>
<变量说明> → integer <变量>
<变量> → <标识符>
<标识符> -> 
<函数说明> → integer function <标识符>（<参数>）；<函数体>
<参数> → <变量>
<函数体> → begin <说明语句表>；<执行语句表> end
<执行语句表> → <执行语句><执行语句表_s>
<执行语句表_s> -> ;<执行语句><执行语句表_s>|<空>
<执行语句> → <读语句>│<写语句>│<赋值语句>│<条件语句>
<读语句> → read(<变量>)
<写语句> → write(<变量>)
<赋值语句> → <变量>:=<算术表达式>
<算术表达式> → <项><算术表达式_s>
<算术表达式_s> -> -<项><算术表达式_s>|<空>
<项> → <因子><项_s>
<项_s> -> *<因子><项_s>|<空>
<因子> → <变量>│<常数>│<函数调用>
<常数> → <无符号整数>
<无符号整数> → <数字><无符号整数_s>
<无符号整数_s> -> <数字><无符号整数_s>|<空>
<函数调用> -> <标识符>(<算术表达式>)
<条件语句> → if<条件表达式>then<执行语句>else <执行语句>
<条件表达式> → <算术表达式><关系运算符><算术表达式>
<关系运算符>  → 
```